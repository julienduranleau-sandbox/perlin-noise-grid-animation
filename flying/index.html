<!DOCTYPE html>
<html>
    <head>
        <title>Perlin Noise Grid Animation - Flying</title>
        <style>
            html, body {
                padding: 0px;
                margin: 0px;
                overflow: hidden;
            }
        </style>
    </head>
    <body>
        <script src="libs/stats.min.js"></script>
        <script src="libs/three.min.js"></script>
        <script src="libs/OrbitControls.js"></script>
        <script src="index.js"></script>

        <script type="custom"  id="vertexShader">
        varying vec3 pos;
        uniform float yoff;

        vec2 fade(vec2 t) {return t*t*t*(t*(t*6.0-15.0)+10.0);}
        vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}

        float cnoise(vec2 P){
          vec4 Pi = floor(P.xyxy) + vec4(0.0, 0.0, 1.0, 1.0);
          vec4 Pf = fract(P.xyxy) - vec4(0.0, 0.0, 1.0, 1.0);
          Pi = mod(Pi, 289.0); // To avoid truncation effects in permutation
          vec4 ix = Pi.xzxz;
          vec4 iy = Pi.yyww;
          vec4 fx = Pf.xzxz;
          vec4 fy = Pf.yyww;
          vec4 i = permute(permute(ix) + iy);
          vec4 gx = 2.0 * fract(i * 0.0243902439) - 1.0; // 1/41 = 0.024...
          vec4 gy = abs(gx) - 0.5;
          vec4 tx = floor(gx + 0.5);
          gx = gx - tx;
          vec2 g00 = vec2(gx.x,gy.x);
          vec2 g10 = vec2(gx.y,gy.y);
          vec2 g01 = vec2(gx.z,gy.z);
          vec2 g11 = vec2(gx.w,gy.w);
          vec4 norm = 1.79284291400159 - 0.85373472095314 *
            vec4(dot(g00, g00), dot(g01, g01), dot(g10, g10), dot(g11, g11));
          g00 *= norm.x;
          g01 *= norm.y;
          g10 *= norm.z;
          g11 *= norm.w;
          float n00 = dot(g00, vec2(fx.x, fy.x));
          float n10 = dot(g10, vec2(fx.y, fy.y));
          float n01 = dot(g01, vec2(fx.z, fy.z));
          float n11 = dot(g11, vec2(fx.w, fy.w));
          vec2 fade_xy = fade(Pf.xy);
          vec2 n_x = mix(vec2(n00, n01), vec2(n10, n11), fade_xy.x);
          float n_xy = mix(n_x.x, n_x.y, fade_xy.y);
          return 2.3 * n_xy;
        }

        void main() {
            float sharpness = 0.05;
            float amplitude = 10.0;
            pos = position;
            vec3 p = position;

            p.z = cnoise(vec2(p.x * sharpness, yoff + p.y * sharpness)) * amplitude;
            float alpha = 1.0;

        	vec4 modelViewPosition = modelViewMatrix * vec4(p, alpha);
        	gl_Position = projectionMatrix * modelViewPosition;
        }


        </script>


        <script type="custom" id="fragmentShader">
        varying vec3 pos;
        uniform float yoff;

        void main() {
            float fadeSize = 5.0;
            float gray = 0.02;
            float lineMoveSpeed = 100.0;
            float spacing = 5.0;

            vec3 rgb = vec3(gray, gray, gray);
            float alpha = 1.0;

            float invertedY = 32.0 - pos.y;
            if (invertedY < fadeSize) {
                alpha = invertedY / fadeSize;
            }

            float xtest = sin(pos.x * spacing);
            float ytest = sin(yoff * lineMoveSpeed + pos.y * spacing);
            float testLimit = 0.98;

            if (xtest > testLimit || ytest > testLimit) {
                rgb = vec3(0.0, 0.15, 0.2) * 1.4;
            }

        	gl_FragColor = vec4(rgb, alpha);
        }
        </script>


        <script type="text/javascript">
            new Game()
        </script>
    </body>
</html>
